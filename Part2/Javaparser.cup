import java_cup.runtime.*;

parser code {:
    // Connect this parser to a scanner!
    Scanner s;
    Parser2(Scanner s){ this.s=s; }
:}

scan with {: return s.next_token(); :};



/*Terminals*/
terminal            PREFIX,SUFFIX,EQ,CONCAT, LPAREN, RPAREN,LBRAC,RBRAC,COMMA,IF,ELSE,REVERSE;
terminal String     STRING_LITERAL,IDENTIFIER;     

/*  Non terminals */
non terminal            prog,e,expr,funcall,fundef,if_stmt,arg,rev,main,expr_list,arg2,if_fun,var;
non terminal String id;


precedence left IF, ELSE;
precedence left PREFIX;       
precedence left CONCAT;    
precedence right REVERSE;  


/*Grammar rules*/
prog ::= e:e1 {: System.out.println("import java.io.*;\n\n"+"class Translated{\n\t"+ e1 + "}" ); :}
        |
;
e ::= main:m {:RESULT = "public static void main(String[] args) {\n\t" + m + "\n}\n";:}
    | fundef:f e:e1 {: RESULT = "public static String  "+ f + "\n" + e1; :} // Allow multiple function definitions
    | fundef:f {: RESULT = "public static String "+ f + "\n"; :}
    ;
main ::= funcall:f1 main:m {: RESULT = "System.out.println(" + f1 +")"+ ";"+ "\n\t" + m; :}
        | funcall:f1 {: RESULT = "System.out.println(" + f1 +")"+ ";";  :}
;
var ::=  IDENTIFIER:i {: RESULT  = i; :}
        | rev:r IDENTIFIER:i {: RESULT  = r + i; :}
        | if_fun:iff {: RESULT  = iff; :}
        | var:v1 CONCAT var:v2 {: RESULT  = v1 + "+" + v2; :}
        | expr:e {: RESULT  = e; :}
;
expr ::= if_stmt:ifst {: RESULT  = ifst; :}
        | funcall:f {: RESULT  = f; :}
        | STRING_LITERAL:i {: RESULT  = "\""+i+"\""; :}
        | expr:e1 CONCAT expr:e2 {: RESULT  = e1 + "+" + e2; :}
        | rev:r STRING_LITERAL:i {: RESULT  = r + "\""+i+"\""; :}
;
funcall ::= IDENTIFIER:i LPAREN arg:a RPAREN {: RESULT = i+"(" + a + ")"; :}
          | IDENTIFIER:i LPAREN RPAREN {: RESULT = i+"()"; :}
;

fundef ::= IDENTIFIER:i LPAREN arg2:a RPAREN LBRAC var:e1 RBRAC {: RESULT = i + "(" + a + ")" + "{" + "\n\t" + "return " + e1 + ";"+ "\n" + "}"; :}
         | IDENTIFIER:i LPAREN  RPAREN LBRAC var:e1 RBRAC {: RESULT = i + "(" + ")" + "{" + "\n\t" + "return " + e1 + ";" + "\n" + "}"; :}
;

if_stmt ::= IF LPAREN expr:e1 PREFIX expr:e2 RPAREN expr:e3 ELSE expr:e4 {: RESULT = "("+ e2 + ".startsWith(" + e1 + ")" + ")" + "?" + "("+ e3 +")"+ ":"  + "("+ e4 +")";   :}         
;
if_fun ::= IF LPAREN var:v1 PREFIX var:v2 RPAREN var:v3 ELSE var:v4 {: RESULT = "("+ v2 + ".startsWith(" + v1 + ")" + ")" + "?" + "("+ v3 +")"+ ":"  + "("+ v4 +")";   :}        
;
arg2 ::= IDENTIFIER:i COMMA arg2:a  {: RESULT = "String " + i + ", " + a; :}
       | IDENTIFIER:i {: RESULT = "String " + i; :}
;
arg ::= expr:e1 COMMA arg:a {: RESULT = e1 + "," + a; :}
       | expr:e1 {: RESULT = e1; :}
;
rev ::= REVERSE rev:r {:RESULT = "reverse " + r;:}
      | REVERSE  {:RESULT = "reverse ";:}
;
id ::= IDENTIFIER:i {: RESULT = i; :}
      | STRING_LITERAL:s {: RESULT = "\"" + s + "\"";:}
;